title Conservation of Momentum
author Menderbug
homepage menderbug.itch.io

========
OBJECTS
========

Background 
GREEN  

Target 
DarkBlue    

Wall 
BROWN

Player 
Blue   

WoodenCrate 
Orange darkbrown
11111
10001
10001
10001
11111

MetalCrate
Gray Darkgray
11111
10001
10001
10001
11111

MomentumR
transparent

MomentumL
transparent

MomentumU
transparent

MomentumD
transparent

=======
LEGEND
=======

. = Background
# = Wall
P = Player
* = WoodenCrate
x = MetalCrate
O = Target

Crate = WoodenCrate or MetalCrate
Crate1 = Crate
Crate2 = Crate

Momentum = MomentumR or MomentumL or MomentumU or MomentumD

=======
SOUNDS
=======

================
COLLISIONLAYERS
================

Background
Target
Player, Wall, Crate
Momentum

======
RULES     
======     

( Player can push one more crates )
[ > Player | Crate ] -> [ > Player | > Crate ]
[ > Crate1 | Crate2 ] -> [ > Crate1 | > Crate2 ]

( Recover crate movement from existing momentum )
[ Crate MomentumR ] -> [ right Crate MomentumR ]
[ Crate MomentumL ] -> [ left Crate MomentumL ]
[ Crate MomentumU ] -> [ up Crate MomentumU ]
[ Crate MomentumD ] -> [ down Crate MomentumD ]

( Add momentum to moving crates )
[ right Crate ] -> [ right Crate right MomentumR ]
[ left Crate ] -> [ left Crate left MomentumL ]
[ up Crate ] -> [ up Crate up MomentumU ]
[ down Crate ] -> [ down Crate down MomentumD ]

(############ late rules ############)

( Transfer momentum through stationary crates, a la Newton's Cradle )
late right [ Crate1 MomentumR | Crate2 no Momentum ] -> [ Crate1 | Crate2 MomentumR ]
late left [ Crate1 MomentumL | Crate2 no Momentum ] -> [ Crate1 | Crate2 MomentumL ]
late up [ Crate1 MomentumU | Crate2 no Momentum ] -> [ Crate1 | Crate2 MomentumU ]
late down [ Crate1 MomentumD | Crate2 no Momentum ] -> [ Crate1 | Crate2 MomentumD ]

( Walls absorb momentum )
late right [ MomentumR | Wall ] -> [ | Wall ]
late left [ MomentumL | Wall ] -> [ | Wall ]
late up [ MomentumU | Wall ] -> [ | Wall ]
late down [ MomentumD | Wall ] -> [ | Wall ]

( If any momentum is left, run another turn )
late [ Momentum ] -> [ Momentum ] again

==============
WINCONDITIONS
==============

All Target on Crate   

=======     
LEVELS
=======

##########
#...##..##
#.p...*.##
#...##...#
######.#.#
#...##...#
#.o.....##
#...######
##########

########################
#O.P..*...**...*...**..#
########################