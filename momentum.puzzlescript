title Isaac and the Conservation of Momentum
author Menderbug
homepage menderbug.itch.io
background_color #3e3546
text_color #8ff8e2

noaction

run_rules_on_level_start

========
OBJECTS
========

Background
#547e64 #92a984
00000
00000
00100
00000
00000

Target
#8ff8e2
.000.
0...0
0.0.0
0...0
.000.

Wall
#3e3546
00000
00000
00000
00000
00000
00000
00000

( RLUD indicate which sides border non-wall tiles )

WallDecoR
#3e3546 #625565 #7f708a #694f62
00012
00012
00002
00012
00012
.....
.....

WallDecoL
#3e3546 #625565 #7f708a #694f62
21000
21000
20000
21000
21000
.....
.....

WallDecoU
#3e3546 #625565 #7f708a #694f62
22222
11011
00000
00000
00000
.....
.....

WallDecoD
#3e3546 #625565 #7f708a #694f62
00000
00000
00000
11011
22222
33333
33333

WallDecoRL
#3e3546 #625565 #7f708a #694f62
21012
21012
20002
21012
21012
.....
.....

WallDecoUD
#3e3546 #625565 #7f708a #694f62
22222
11011
00000
11011
22222
33333
33333

WallDecoRU
#3e3546 #625565 #7f708a #694f62
22222
11012
00002
00012
00012
.....
.....

WallDecoRD
#3e3546 #625565 #7f708a #694f62
00012
00012
00002
11012
22222
33333
33333

WallDecoLU
#3e3546 #625565 #7f708a #694f62
22222
21011
20000
21000
21000
.....
.....

WallDecoLD
#3e3546 #625565 #7f708a #694f62
21000
21000
20000
21011
22222
33333
33333

WallDecoRLU
#3e3546 #625565 #7f708a #694f62
22222
21012
20002
21012
21012
.....
.....

WallDecoRLD
#3e3546 #625565 #7f708a #694f62
21012
21012
20002
21012
22222
33333
33333

WallDecoUDR
#3e3546 #625565 #7f708a #694f62
22222
11012
00002
11012
22222
33333
33333

WallDecoUDL
#3e3546 #625565 #7f708a #694f62
22222
21011
20000
21011
22222
33333
33333

WallDecoRLUD
#3e3546 #625565 #7f708a #694f62
22222
21012
20002
21012
22222
33333
33333

WallDecoCornerRU
#3e3546 #625565 #7f708a #694f62
...12
...11
.....
.....
.....
.....
.....

WallDecoCornerRD
#3e3546 #625565 #7f708a #694f62
.....
.....
.....
...11
...12
.....
.....

WallDecoCornerLU
#3e3546 #625565 #7f708a #694f62
21...
11...
.....
.....
.....
.....
.....

WallDecoCornerLD
#3e3546 #625565 #7f708a #694f62
.....
.....
.....
11...
21...
.....
.....

Player
#2e222f #3e3546 #7f708a #c7dcd0 #ffffff #fdcbb0 #323353
.333.
34433
36563
35553
.021.
.001.

Pit
#1a1518

PitDeco
#2e222f #3e3546
11111
00000
.....
.....
.....

WoodenCrate
#6e2727 #7a3045 #9e4539 #cd683d
.222.
23220
22210
21100
.000.
.....

FallingWoodenCrate1
#6e2727 #7a3045 #9e4539 #cd683d
.222.
23220
22210
21100
.000.
.....

FallingWoodenCrate2
#6e2727 #7a3045 #9e4539 #cd683d
..2..
.210.
..0..

FallingWoodenCrate3
#6e2727 #7a3045 #9e4539 #cd683d
..1..

MetalCrate
Gray Darkgray
11111
10001
10001
10001
11111

FallingMetalCrate1
Gray Darkgray
.....
.111.
.101.
.111.
.....

FallingMetalCrate2
Gray Darkgray
.....
.....
..1..
.....
.....

( The numbers are units )
Momentum1R
transparent

Momentum1L
transparent

Momentum1U
transparent

Momentum1D
transparent

Momentum2R
transparent

Momentum2L
transparent

Momentum2U
transparent

Momentum2D
transparent

_WallEndsR
pink

_WallEndsL
pink

_WallEndsU
pink

_WallEndsD
pink

_Temp
pink

=======
LEGEND
=======

. = Background
_ = Pit
# = Wall
P = Player
* = WoodenCrate
x = MetalCrate
O = Target

Crate = WoodenCrate or MetalCrate
Crate1 = Crate
Crate2 = Crate

FallingWoodenCrate = FallingWoodenCrate1 or FallingWoodenCrate2 or FallingWoodenCrate3
FallingMetalCrate = FallingMetalCrate1 or FallingMetalCrate2
FallingCrate = FallingWoodenCrate or FallingMetalCrate

Momentum1 = Momentum1R or Momentum1L or Momentum1U or Momentum1D
Momentum2 = Momentum2R or Momentum2L or Momentum2U or Momentum2D
MomentumR = Momentum1R or Momentum2R
MomentumL = Momentum1L or Momentum2L
MomentumU = Momentum1U or Momentum2U
MomentumD = Momentum1D or Momentum2D
Momentum = Momentum1 or Momentum2

WallEnds = _WallEndsR or _WallEndsL or _WallEndsU or _WallEndsD
WallDeco = WallDecoR or WallDecoL or WallDecoU or WallDecoD or WallDecoRL or WallDecoUD or WallDecoRU or WallDecoRD or WallDecoLU or WallDecoLD or WallDecoRLU or WallDecoRLD or WallDecoUDR or WallDecoUDL or WallDecoRLUD
WallDecoCorner = WallDecoCornerRU or WallDecoCornerRD or WallDecoCornerLU or WallDecoCornerLD

=======
SOUNDS
=======

sfx0 61949504 ( wooden ball )

sfx1 71577900 ( metal ball )

sfx2 48227904 ( ball hits wall )

sfx9 64621908 ( object falling into pit )

sfx10 19886102 ( player death? )

endlevel 27401703

================
COLLISIONLAYERS
================

Background
Pit
PitDeco
FallingCrate
Target
Player, Wall, Crate
WallDeco
WallDecoCornerRU
WallDecoCornerRD
WallDecoCornerLU
WallDecoCornerLD
Momentum
_WallEndsR
_WallEndsL
_WallEndsU
_WallEndsD
_Temp

======
RULES
======

( Visuals )
up [ Pit no PitDeco | no Pit ] -> [ Pit PitDeco | ]
right [ Wall no WallDecoR | no Wall ] -> [ Wall _WallEndsR | ]
left [ Wall no WallDecoR | no Wall ] -> [ Wall _WallEndsL | ]
up [ Wall no WallDecoR | no Wall ] -> [ Wall _WallEndsU | ]
down [ Wall no WallDecoR | no Wall ] -> [ Wall _WallEndsD | ]

right [ Wall no _WallEndsU | _WallEndsU ] -> [ Wall _Temp | _WallEndsU ]
up [ _Temp | _WallEndsR ] -> [ _Temp WallDecoCornerRU | _WallEndsR ]
[ _Temp ] -> [ ]
right [ Wall no _WallEndsD | _WallEndsD ] -> [ Wall _Temp | _WallEndsD ]
down [ _Temp | _WallEndsR ] -> [ _Temp WallDecoCornerRD | _WallEndsR ]
[ _Temp ] -> [ ]
left [ Wall no _WallEndsU | _WallEndsU ] -> [ Wall _Temp | _WallEndsU ]
up [ _Temp | _WallEndsL ] -> [ _Temp WallDecoCornerLU | _WallEndsL ]
[ _Temp ] -> [ ]
left [ Wall no _WallEndsD | _WallEndsD ] -> [ Wall _Temp | _WallEndsD ]
down [ _Temp | _WallEndsL ] -> [ _Temp WallDecoCornerLD | _WallEndsL ]
[ _Temp ] -> [ ]

[ _WallEndsR _WallEndsL _WallEndsU _WallEndsD ] -> [ WallDecoRLUD ]
[ _WallEndsR _WallEndsL _WallEndsU ] -> [ WallDecoRLU ]
[ _WallEndsR _WallEndsL _WallEndsD ] -> [ WallDecoRLD ]
[ _WallEndsR _WallEndsU _WallEndsD ] -> [ WallDecoUDR ]
[ _WallEndsL _WallEndsU _WallEndsD ] -> [ WallDecoUDL ]
[ _WallEndsR _WallEndsL ] -> [ WallDecoRL ]
[ _WallEndsU _WallEndsD ] -> [ WallDecoUD ]
[ _WallEndsR _WallEndsU ] -> [ WallDecoRU ]
[ _WallEndsR _WallEndsD ] -> [ WallDecoRD ]
[ _WallEndsL _WallEndsU ] -> [ WallDecoLU ]
[ _WallEndsL _WallEndsD ] -> [ WallDecoLD ]
[ _WallEndsR ] -> [ WallDecoR ]
[ _WallEndsL ] -> [ WallDecoL ]
[ _WallEndsU ] -> [ WallDecoU ]
[ _WallEndsD ] -> [ WallDecoD ]


( Player can't walk over pits )
[ > Player | Pit ] -> [ Player | Pit ]

( Player can push one more crates. )
[ > Player | Crate ] -> [ > Player | > Crate ]
[ > Crate1 | Crate2 ] -> [ > Crate1 | > Crate2 ]

( Recover crate movement from existing momentum. )
[ Crate MomentumR ] -> [ right Crate MomentumR ]
[ Crate MomentumL ] -> [ left Crate MomentumL ]
[ Crate MomentumU ] -> [ up Crate MomentumU ]
[ Crate MomentumD ] -> [ down Crate MomentumD ]

( Add momentum to moving crates. )
[ right WoodenCrate ] -> [ right WoodenCrate right Momentum1R ]
[ left WoodenCrate ] -> [ left WoodenCrate left Momentum1L ]
[ up WoodenCrate ] -> [ up WoodenCrate up Momentum1U ]
[ down WoodenCrate ] -> [ down WoodenCrate down Momentum1D ]
[ right MetalCrate ] -> [ right MetalCrate right Momentum2R ]
[ left MetalCrate ] -> [ left MetalCrate left Momentum2L ]
[ up MetalCrate ] -> [ up MetalCrate up Momentum2U ]
[ down MetalCrate ] -> [ down MetalCrate down Momentum2D ]

(############ late rules ############)

( Sound effects )
late right [ Crate MomentumR | WoodenCrate no Momentum ] -> [ Crate MomentumR | WoodenCrate ] sfx0
late left [ Crate MomentumL | WoodenCrate no Momentum ] -> [ Crate MomentumL | WoodenCrate ] sfx0
late up [ Crate MomentumU | WoodenCrate no Momentum ] -> [ Crate MomentumU | WoodenCrate ] sfx0
late down [ Crate MomentumD | WoodenCrate no Momentum ] -> [ Crate MomentumD | WoodenCrate ] sfx0
late right [ Crate MomentumR | MetalCrate no Momentum ] -> [ Crate MomentumR | MetalCrate ] sfx1
late left [ Crate MomentumL | MetalCrate no Momentum ] -> [ Crate MomentumL | MetalCrate ] sfx1
late up [ Crate MomentumU | MetalCrate no Momentum ] -> [ Crate MomentumU | MetalCrate ] sfx1
late down [ Crate MomentumD | MetalCrate no Momentum ] -> [ Crate MomentumD | MetalCrate ] sfx1
late right [ Crate MomentumR | Wall ] -> [ Crate MomentumR | Wall ] sfx2
late left [ Crate MomentumL | Wall ] -> [ Crate MomentumL | Wall ] sfx2
late up [ Crate MomentumU | Wall ] -> [ Crate MomentumU | Wall ] sfx2
late down [ Crate MomentumD | Wall ] -> [ Crate MomentumD | Wall ] sfx2


( Transfer momentum through stationary crates, a la Newton's Cradle.
  Metal crates can store two units of momentum.
  Walls absorb momentum. )  
late right [ Crate1 Momentum1R | Crate2 no Momentum ] -> [ Crate1 | Crate2 Momentum1R ]
+ late right [ Crate Momentum1R | MetalCrate Momentum1R ] -> [ Crate | MetalCrate Momentum2R ]
+ late right [ MetalCrate Momentum2R | Crate no Momentum ] -> [ MetalCrate Momentum1R | Crate Momentum1R ]
+ late right [ MetalCrate Momentum2R | MetalCrate Momentum1R ] -> [ MetalCrate Momentum1R | MetalCrate Momentum2R ]
+ late right [ Crate MomentumR | Wall ] -> [ Crate | Wall ]
late left [ Crate1 Momentum1L | Crate2 no Momentum ] -> [ Crate1 | Crate2 Momentum1L ]
+ late left [ Crate Momentum1L | MetalCrate Momentum1L ] -> [ Crate | MetalCrate Momentum2L ]
+ late left [ MetalCrate Momentum2L | Crate no Momentum ] -> [ MetalCrate Momentum1L | Crate Momentum1L ]
+ late left [ MetalCrate Momentum2L | MetalCrate Momentum1L ] -> [ MetalCrate Momentum1L | MetalCrate Momentum2L ]
+ late left [ Crate MomentumL | Wall ] -> [ Crate | Wall ]
late up [ Crate1 Momentum1U | Crate2 no Momentum ] -> [ Crate1 | Crate2 Momentum1U ]
+ late up [ Crate Momentum1U | MetalCrate Momentum1U ] -> [ Crate | MetalCrate Momentum2U ]
+ late up [ MetalCrate Momentum2U | Crate no Momentum ] -> [ MetalCrate Momentum1U | Crate Momentum1U ]
+ late up [ MetalCrate Momentum2U | MetalCrate Momentum1U ] -> [ MetalCrate Momentum1U | MetalCrate Momentum2U ]
+ late up [ Crate MomentumU | Wall ] -> [ Crate | Wall ]
late down [ Crate1 Momentum1D | Crate2 no Momentum ] -> [ Crate1 | Crate2 Momentum1D ]
+ late down [ Crate Momentum1D | MetalCrate Momentum1D ] -> [ Crate | MetalCrate Momentum2D ]
+ late down [ MetalCrate Momentum2D | Crate no Momentum ] -> [ MetalCrate Momentum1D | Crate Momentum1D ]
+ late down [ MetalCrate Momentum2D | MetalCrate Momentum1D ] -> [ MetalCrate Momentum1D | MetalCrate Momentum2D ]
+ late down [ Crate MomentumD | Wall ] -> [ Crate | Wall ]

( Metal crates require two units of momentum to keep moving. )
late [ MetalCrate Momentum1 ] -> [ MetalCrate ]

( Drop crates into pits. )
late [ FallingWoodenCrate3 ] -> [ ]
late [ FallingWoodenCrate2 Pit ] -> [ FallingWoodenCrate3 Pit ] again
late [ FallingWoodenCrate1 Pit ] -> [ FallingWoodenCrate2 Pit ] again
late [ WoodenCrate Pit ] -> [ FallingWoodenCrate1 Pit ] sfx9 again
late [ FallingMetalCrate2 ] -> [ ]
late [ FallingMetalCrate1 Pit ] -> [ FallingMetalCrate2 Pit ] again
late [ MetalCrate Pit ] -> [ FallingMetalCrate1 Pit ] sfx9 again

( Remove stray momentum. )
late [ Momentum no Crate ] -> [ ]

( If any momentum is left, run another turn. )
late [ Momentum ] -> [ Momentum ] again

==============
WINCONDITIONS
==============

All Target on Crate

No Momentum

No FallingCrate

=======
LEVELS
=======

( Introduces momentum )

###########
#...#######
#.p.#######
#...#######
##.########
##.####...#
#.*..o..#.#
#...#.....#
###...#####
###########

( Introduces momentum transfer )

###########
###...oo..#
###.*.....#
#....*..#.#
#..p......#
##.....####
##..#######
###########

###########
#...*..o..#
#.p.*.....#
#...*.....#
######...##
###########

###########
###......##
#..*.o.*.##
#.........#
##.*.o.p..#
##......###
###########

( Introduces pushing multiple crates at once )

#########
#....o..#
#.###o#.#
#.###...#
#.**..p##
####...##
#########

###########
###......o#
#........o#
#.p.***..o#
#.......###
###########

##########
####o#o#o#
####.#.#.#
#........#
#..#...p.#
##.*....##
##.*....##
###.*..###
###..#####
##########

( Introduces pits )

##########
#P.*...o_#
###.#....#
##....#..#
##.*.....#
#.......##
#....#..##
###....###
##########

( Introduces stopping over a pit )

##########
######o###
######.###
###......#
#...#....#
#.p...._##
#..#.*...#
##..*....#
##......##
##########

( Introduces dropping several crates into a pit at once )

###############
###......######
##..#....__####
##p.***.o._.###
#..#..........#
#..........#..#
###...........#
###..........##
####......#..##
#######...#####
###############

( Introduces momentum transfer to stop over a pit )

############
#####..#####
#####......#
###.....#..#
###.*......#
#....o..p..#
#.*..._...##
####...#..##
####.*....##
####.....###
############

( Introduces momentum transfer of multiple crates )

###############
#.....o......##
#p####.#####.##
#..**...***..o#
######.#####.##
######.......##
###############

##############
###..###...###
###.........##
##.......*..##
##.........###
#..##...######
#..#..####___#
#....._***.o_#
#...*.####___#
###.....######
###..#.p.#####
####.....#####
##############

###############
#####_#_#_#####
#####o#o#o#####
#####o#o#o#####
#####o#o#o#####
##..#o#o#o#..##
##...........##
##.#.*****.#.##
#.........#...#
#.p..*****....#
#...#.........#
###..*****..###
####.......####
###############
###############

message Thank you for playing! :)


(

########################
#O.P..***.****.*._.*...#
########################

)