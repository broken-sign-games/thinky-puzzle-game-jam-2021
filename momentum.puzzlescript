title Isaac and the Conservation of Momentum
author Menderbug
homepage menderbug.itch.io
background_color #3e3546
text_color #8ff8e2

run_rules_on_level_start
norepeat_action

========
OBJECTS
========

Background
#547e64 #92a984
00000
00000
00100
00000
00000

Target
#8ff8e2
.000.
0...0
0.0.0
0...0
.000.

Wall
#3e3546
00000
00000
00000
00000
00000
00000
00000

( RLUD indicate which sides border non-wall tiles )

WallDecoR
#3e3546 #625565 #7f708a #694f62
00012
00012
00002
00012
00012
.....
.....

WallDecoL
#3e3546 #625565 #7f708a #694f62
21000
21000
20000
21000
21000
.....
.....

WallDecoU
#3e3546 #625565 #7f708a #694f62
22222
11011
00000
00000
00000
.....
.....

WallDecoD
#3e3546 #625565 #7f708a #694f62
00000
00000
00000
11011
22222
33333
33333

WallDecoRL
#3e3546 #625565 #7f708a #694f62
21012
21012
20002
21012
21012
.....
.....

WallDecoUD
#3e3546 #625565 #7f708a #694f62
22222
11011
00000
11011
22222
33333
33333

WallDecoRU
#3e3546 #625565 #7f708a #694f62
22222
11012
00002
00012
00012
.....
.....

WallDecoRD
#3e3546 #625565 #7f708a #694f62
00012
00012
00002
11012
22222
33333
33333

WallDecoLU
#3e3546 #625565 #7f708a #694f62
22222
21011
20000
21000
21000
.....
.....

WallDecoLD
#3e3546 #625565 #7f708a #694f62
21000
21000
20000
21011
22222
33333
33333

WallDecoRLU
#3e3546 #625565 #7f708a #694f62
22222
21012
20002
21012
21012
.....
.....

WallDecoRLD
#3e3546 #625565 #7f708a #694f62
21012
21012
20002
21012
22222
33333
33333

WallDecoUDR
#3e3546 #625565 #7f708a #694f62
22222
11012
00002
11012
22222
33333
33333

WallDecoUDL
#3e3546 #625565 #7f708a #694f62
22222
21011
20000
21011
22222
33333
33333

WallDecoRLUD
#3e3546 #625565 #7f708a #694f62
22222
21012
20002
21012
22222
33333
33333

WallDecoCornerRU
#3e3546 #625565 #7f708a #694f62
...12
...11
.....
.....
.....
.....
.....

WallDecoCornerRD
#3e3546 #625565 #7f708a #694f62
.....
.....
.....
...11
...12
.....
.....

WallDecoCornerLU
#3e3546 #625565 #7f708a #694f62
21...
11...
.....
.....
.....
.....
.....

WallDecoCornerLD
#3e3546 #625565 #7f708a #694f62
.....
.....
.....
11...
21...
.....
.....

Sand
#fbff86 #d5e04b
00000
00000
00100
00000
00000

SandDecoR
#fbff86 #d5e04b
.....
.....
....0
....0
.....

SandDecoL
#fbff86 #d5e04b
.....
0....
0....
.....
.....

SandDecoU
#fbff86 #d5e04b
..00.
.....
.....
.....
.....

SandDecoD
#fbff86 #d5e04b
.....
.....
.....
.....
.00..

SandDecoBall
#fbff86 #d5e04b
.....
.....
.....
..11.
.....

Player
#2e222f #3e3546 #7f708a #c7dcd0 #ffffff #fdcbb0 #323353
.333.
34433
36563
35553
.021.
.001.

FallingPlayer1
#2e222f #3e3546 #7f708a #c7dcd0 #ffffff #fdcbb0 #323353
.333.
34433
36563
35553
.021.
.001.

FallingPlayer2
#2e222f #3e3546 #7f708a #c7dcd0 #ffffff #fdcbb0 #323353
.433.
.353.
.555.
.021.

FallingPlayer3
#2e222f #3e3546 #7f708a #c7dcd0 #ffffff #fdcbb0 #323353
..3..
..1..

Pit
#1a1518

PitDeco
#2e222f #3e3546
11111
00000
.....
.....
.....

WoodenBall
#6e2727 #7a3045 #9e4539 #cd683d
.222.
23221
22210
21100
.000.
.....

FallingWoodenBall1
#6e2727 #7a3045 #9e4539 #cd683d
.222.
23221
22210
21100
.000.
.....

FallingWoodenBall2
#6e2727 #7a3045 #9e4539 #cd683d
.222.
.210.
.000.

FallingWoodenBall3
#6e2727 #7a3045 #9e4539 #cd683d
..0..

MetalBall
#625565 #9babb2 #c7dcd0 #ffffff
.222.
23221
22210
21100
.000.
.....

FallingMetalBall1
#625565 #9babb2 #c7dcd0 #ffffff
.222.
23221
22210
21100
.000.
.....

FallingMetalBall2
#625565 #9babb2 #c7dcd0 #ffffff
.222.
.210.
.000.

FallingMetalBall3
#625565 #9babb2 #c7dcd0 #ffffff
..0..

( The numbers are units )
Momentum1R
transparent

Momentum1L
transparent

Momentum1U
transparent

Momentum1D
transparent

Momentum2R
transparent

Momentum2L
transparent

Momentum2U
transparent

Momentum2D
transparent

LevelSkip0
transparent

LevelSkip1
transparent

LevelSkip2
transparent

_WallEndsR
pink

_WallEndsL
pink

_WallEndsU
pink

_WallEndsD
pink

_Temp
pink

=======
LEGEND
=======

. = Background
~ = Sand
_ = Pit
# = Wall
P = Player
* = WoodenBall
x = MetalBall
O = Target
Q = Target and WoodenBall

Ball = WoodenBall or MetalBall
Ball1 = Ball
Ball2 = Ball

FallingWoodenBall = FallingWoodenBall1 or FallingWoodenBall2 or FallingWoodenBall3
FallingMetalBall = FallingMetalBall1 or FallingMetalBall2 or FallingMetalBall3
FallingBall = FallingWoodenBall or FallingMetalBall
FallingPlayer = FallingPlayer1 or FallingPlayer2 or FallingPlayer3

Momentum1 = Momentum1R or Momentum1L or Momentum1U or Momentum1D
Momentum2 = Momentum2R or Momentum2L or Momentum2U or Momentum2D
MomentumR = Momentum1R or Momentum2R
MomentumL = Momentum1L or Momentum2L
MomentumU = Momentum1U or Momentum2U
MomentumD = Momentum1D or Momentum2D
Momentum = Momentum1 or Momentum2

WallEnds = _WallEndsR or _WallEndsL or _WallEndsU or _WallEndsD
WallDeco = WallDecoR or WallDecoL or WallDecoU or WallDecoD or WallDecoRL or WallDecoUD or WallDecoRU or WallDecoRD or WallDecoLU or WallDecoLD or WallDecoRLU or WallDecoRLD or WallDecoUDR or WallDecoUDL or WallDecoRLUD
WallDecoCorner = WallDecoCornerRU or WallDecoCornerRD or WallDecoCornerLU or WallDecoCornerLD

LevelSkip = LevelSkip0 or LevelSkip1 or LevelSkip2

=======
SOUNDS
=======

Player move 39983908

sfx0 61949504 ( ball hits wooden ball )

sfx1 71577900 ( ball hits metal ball )

sfx2 48227904 ( ball hits wall )

sfx7 36743507 ( player steps onto sand )

sfx8 59530308 ( ball hits sand )

sfx9 96780902 (726108) (83188902) (64621908) ( object falls into pit ) ( 89526908 )

sfx10 19886102 ( player falls  )

endlevel 27401703

( pushing a crate over something? 92610308 )
( momentum redirection? 93693708 )

================
COLLISIONLAYERS
================

Background
Sand
SandDecoR
SandDecoL
SandDecoU
SandDecoD
Pit
PitDeco
FallingBall
FallingPlayer
Target
Player, Wall, Ball
SandDecoBall
WallDeco
WallDecoCornerRU
WallDecoCornerRD
WallDecoCornerLU
WallDecoCornerLD
Momentum
LevelSkip
_WallEndsR
_WallEndsL
_WallEndsU
_WallEndsD
_Temp

======
RULES
======

( Skipping levels )
random [ ] -> [ _Temp ]
[ LevelSkip ] [ _Temp ] -> [ LevelSkip ] [ ]
[ ] [ _Temp ] -> [ LevelSkip0 ] [ ]
[ LevelSkip ] [ orthogonal Player ] -> [ LevelSkip0 ] [ orthogonal Player ]
[ LevelSkip0 ] [ action Player ] -> [ LevelSkip1 ] [ Player ]
[ LevelSkip1 ] [ action Player ] -> [ LevelSkip2 ] [ Player ] message Pressing X one more time will skip the level. I promise I won't judge you.
[ LevelSkip2 ] [ action Player ] -> win

( Visuals )
up [ Pit no PitDeco | no Pit ] -> [ Pit PitDeco | ]
right [ Wall no WallDecoR | no Wall ] -> [ Wall _WallEndsR | ]
left [ Wall no WallDecoR | no Wall ] -> [ Wall _WallEndsL | ]
up [ Wall no WallDecoR | no Wall ] -> [ Wall _WallEndsU | ]
down [ Wall no WallDecoR | no Wall ] -> [ Wall _WallEndsD | ]

right [ Wall no _WallEndsU | _WallEndsU ] -> [ Wall _Temp | _WallEndsU ]
up [ _Temp | _WallEndsR ] -> [ _Temp WallDecoCornerRU | _WallEndsR ]
[ _Temp ] -> [ ]
right [ Wall no _WallEndsD | _WallEndsD ] -> [ Wall _Temp | _WallEndsD ]
down [ _Temp | _WallEndsR ] -> [ _Temp WallDecoCornerRD | _WallEndsR ]
[ _Temp ] -> [ ]
left [ Wall no _WallEndsU | _WallEndsU ] -> [ Wall _Temp | _WallEndsU ]
up [ _Temp | _WallEndsL ] -> [ _Temp WallDecoCornerLU | _WallEndsL ]
[ _Temp ] -> [ ]
left [ Wall no _WallEndsD | _WallEndsD ] -> [ Wall _Temp | _WallEndsD ]
down [ _Temp | _WallEndsL ] -> [ _Temp WallDecoCornerLD | _WallEndsL ]
[ _Temp ] -> [ ]

[ _WallEndsR _WallEndsL _WallEndsU _WallEndsD ] -> [ WallDecoRLUD ]
[ _WallEndsR _WallEndsL _WallEndsU ] -> [ WallDecoRLU ]
[ _WallEndsR _WallEndsL _WallEndsD ] -> [ WallDecoRLD ]
[ _WallEndsR _WallEndsU _WallEndsD ] -> [ WallDecoUDR ]
[ _WallEndsL _WallEndsU _WallEndsD ] -> [ WallDecoUDL ]
[ _WallEndsR _WallEndsL ] -> [ WallDecoRL ]
[ _WallEndsU _WallEndsD ] -> [ WallDecoUD ]
[ _WallEndsR _WallEndsU ] -> [ WallDecoRU ]
[ _WallEndsR _WallEndsD ] -> [ WallDecoRD ]
[ _WallEndsL _WallEndsU ] -> [ WallDecoLU ]
[ _WallEndsL _WallEndsD ] -> [ WallDecoLD ]
[ _WallEndsR ] -> [ WallDecoR ]
[ _WallEndsL ] -> [ WallDecoL ]
[ _WallEndsU ] -> [ WallDecoU ]
[ _WallEndsD ] -> [ WallDecoD ]

right [ no Sand no SandDecoR no Wall no Pit | Sand ] -> [ SandDecoR | Sand ]
left [ no Sand no SandDecoL no Wall no Pit | Sand ] -> [ SandDecoL | Sand ]
up [ no Sand no SandDecoU no Wall no Pit | Sand ] -> [ SandDecoU | Sand ]
down [ no Sand no SandDecoD no Wall no Pit | Sand ] -> [ SandDecoD | Sand ]

( Movement-based sound effects )
[ > Player | Sand ] -> [ > Player | Sand ] sfx7

( Player can push one more balls. )
[ > Player | Ball ] -> [ > Player | > Ball ]
[ > Ball1 | Ball2 ] -> [ > Ball1 | > Ball2 ]

( Recover ball movement from existing momentum. )
[ Ball MomentumR ] -> [ right Ball MomentumR ]
[ Ball MomentumL ] -> [ left Ball MomentumL ]
[ Ball MomentumU ] -> [ up Ball MomentumU ]
[ Ball MomentumD ] -> [ down Ball MomentumD ]

( Add momentum to moving balls. )
[ right WoodenBall ] -> [ right WoodenBall right Momentum1R ]
[ left WoodenBall ] -> [ left WoodenBall left Momentum1L ]
[ up WoodenBall ] -> [ up WoodenBall up Momentum1U ]
[ down WoodenBall ] -> [ down WoodenBall down Momentum1D ]
[ right MetalBall ] -> [ right MetalBall right Momentum2R ]
[ left MetalBall ] -> [ left MetalBall left Momentum2L ]
[ up MetalBall ] -> [ up MetalBall up Momentum2U ]
[ down MetalBall ] -> [ down MetalBall down Momentum2D ]

(############ late rules ############)

( Sound effects )
late right [ WoodenBall MomentumR | WoodenBall no Momentum ] -> [ WoodenBall MomentumR | WoodenBall ] sfx0
late left [ WoodenBall MomentumL | WoodenBall no Momentum ] -> [ WoodenBall MomentumL | WoodenBall ] sfx0
late up [ WoodenBall MomentumU | WoodenBall no Momentum ] -> [ WoodenBall MomentumU | WoodenBall ] sfx0
late down [ WoodenBall MomentumD | WoodenBall no Momentum ] -> [ WoodenBall MomentumD | WoodenBall ] sfx0
late right [ MetalBall MomentumR | WoodenBall no Momentum ] -> [ MetalBall MomentumR | WoodenBall ] sfx1
late left [ MetalBall MomentumL | WoodenBall no Momentum ] -> [ MetalBall MomentumL | WoodenBall ] sfx1
late up [ MetalBall MomentumU | WoodenBall no Momentum ] -> [ MetalBall MomentumU | WoodenBall ] sfx1
late down [ MetalBall MomentumD | WoodenBall no Momentum ] -> [ MetalBall MomentumD | WoodenBall ] sfx1
late right [ Ball MomentumR | MetalBall no Momentum ] -> [ Ball MomentumR | MetalBall ] sfx1
late left [ Ball MomentumL | MetalBall no Momentum ] -> [ Ball MomentumL | MetalBall ] sfx1
late up [ Ball MomentumU | MetalBall no Momentum ] -> [ Ball MomentumU | MetalBall ] sfx1
late down [ Ball MomentumD | MetalBall no Momentum ] -> [ Ball MomentumD | MetalBall ] sfx1
late right [ Ball MomentumR | Wall ] -> [ Ball MomentumR | Wall ] sfx2
late left [ Ball MomentumL | Wall ] -> [ Ball MomentumL | Wall ] sfx2
late up [ Ball MomentumU | Wall ] -> [ Ball MomentumU | Wall ] sfx2
late down [ Ball MomentumD | Wall ] -> [ Ball MomentumD | Wall ] sfx2
late [ Ball Pit ] -> [ Ball Pit ] sfx9
late [ Player Pit ] -> [ Player Pit ] sfx10
late [ Ball Momentum Sand ] -> [ Ball Momentum Sand ] sfx8

( Dynamic visuals )
late [ SandDecoBall ] -> [ ]
late [ Ball Sand ] -> [ Ball Sand SandDecoBall ]

( Sand absorbs momentum of entering balls. )
late [ Ball Momentum Sand ] -> [ Ball Sand ]

( Transfer momentum through stationary balls, a la Newton's Cradle.
  Metal balls can store two units of momentum.
  Walls absorb momentum. )  
late right [ Ball1 Momentum1R | Ball2 no Momentum ] -> [ Ball1 | Ball2 Momentum1R ]
+ late right [ Ball Momentum1R | MetalBall Momentum1R ] -> [ Ball | MetalBall Momentum2R ]
+ late right [ MetalBall Momentum2R | Ball no Momentum ] -> [ MetalBall Momentum1R | Ball Momentum1R ]
+ late right [ MetalBall Momentum2R | MetalBall Momentum1R ] -> [ MetalBall Momentum1R | MetalBall Momentum2R ]
+ late right [ Ball MomentumR | Wall ] -> [ Ball | Wall ]
late left [ Ball1 Momentum1L | Ball2 no Momentum ] -> [ Ball1 | Ball2 Momentum1L ]
+ late left [ Ball Momentum1L | MetalBall Momentum1L ] -> [ Ball | MetalBall Momentum2L ]
+ late left [ MetalBall Momentum2L | Ball no Momentum ] -> [ MetalBall Momentum1L | Ball Momentum1L ]
+ late left [ MetalBall Momentum2L | MetalBall Momentum1L ] -> [ MetalBall Momentum1L | MetalBall Momentum2L ]
+ late left [ Ball MomentumL | Wall ] -> [ Ball | Wall ]
late up [ Ball1 Momentum1U | Ball2 no Momentum ] -> [ Ball1 | Ball2 Momentum1U ]
+ late up [ Ball Momentum1U | MetalBall Momentum1U ] -> [ Ball | MetalBall Momentum2U ]
+ late up [ MetalBall Momentum2U | Ball no Momentum ] -> [ MetalBall Momentum1U | Ball Momentum1U ]
+ late up [ MetalBall Momentum2U | MetalBall Momentum1U ] -> [ MetalBall Momentum1U | MetalBall Momentum2U ]
+ late up [ Ball MomentumU | Wall ] -> [ Ball | Wall ]
late down [ Ball1 Momentum1D | Ball2 no Momentum ] -> [ Ball1 | Ball2 Momentum1D ]
+ late down [ Ball Momentum1D | MetalBall Momentum1D ] -> [ Ball | MetalBall Momentum2D ]
+ late down [ MetalBall Momentum2D | Ball no Momentum ] -> [ MetalBall Momentum1D | Ball Momentum1D ]
+ late down [ MetalBall Momentum2D | MetalBall Momentum1D ] -> [ MetalBall Momentum1D | MetalBall Momentum2D ]
+ late down [ Ball MomentumD | Wall ] -> [ Ball | Wall ]

( Metal balls require two units of momentum to keep moving. )
late [ MetalBall Momentum1 ] -> [ MetalBall ]

( Drop balls into pits. )
late [ FallingWoodenBall3 ] -> [ ]
late [ FallingWoodenBall2 Pit ] -> [ FallingWoodenBall3 Pit ] again
late [ FallingWoodenBall1 Pit ] -> [ FallingWoodenBall2 Pit ] again
late [ WoodenBall Pit ] -> [ FallingWoodenBall1 Pit ] again
late [ FallingMetalBall3 ] -> [ ]
late [ FallingMetalBall2 Pit ] -> [ FallingMetalBall3 Pit ] again
late [ FallingMetalBall1 Pit ] -> [ FallingMetalBall2 Pit ] again
late [ MetalBall Pit ] -> [ FallingMetalBall1 Pit ] again

( Drop player into pits. )
late [ FallingPlayer3 ] -> [ ]
late [ FallingPlayer2 Pit ] -> [ FallingPlayer3 Pit ] again
late [ FallingPlayer1 Pit ] -> [ FallingPlayer2 Pit ] again
late [ Player Pit ] -> [ FallingPlayer1 Pit ] again

( Remove stray momentum. )
late [ Momentum no Ball ] -> [ ]

( If any momentum is left, run another turn. )
late [ Momentum ] -> [ Momentum ] again

==============
WINCONDITIONS
==============

All Target on Ball

No Momentum

No FallingBall

=======
LEVELS
=======

message If you're stuck, press X three times to skip a level.

( Introduces momentum )

###########
#...#######
#.p.#######
#...#######
##.########
##.####...#
#.*..o..#.#
#...#.....#
###...#####
###########

( Introduces momentum transfer )

###########
###...oo..#
###.*.....#
#....*..#.#
#..p......#
##.....####
##..#######
###########

########
###...##
#..*p.##
#.##*.##
#..#.o##
##....##
#####.##
#####..#
########

###########
#...*..o..#
#.p.*.....#
#...*.....#
######...##
###########

###########
###......##
#..*.o.*.##
#.........#
##.*.o.p..#
##......###
###########

( Introduces pushing multiple balls at once )

#########
#....o..#
#.###o#.#
#.###...#
#.**..p##
####...##
#########

###########
###......o#
#........o#
#.p.***..o#
#.......###
###########

##########
####o#o#o#
####.#.#.#
#........#
#..#...p.#
##.*....##
##.*....##
###.*..###
###..#####
##########

( Introduces pits )

##########
#P.*...o_#
###.#....#
##....#..#
##.*.....#
#.......##
#....#..##
###....###
##########

( Introduces stopping over a pit )

##########
######o###
######.###
###......#
#...#....#
#.p...._##
#..#.*...#
##..*....#
##......##
##########

( Introduces dropping several balls into a pit at once )

#############
###......####
##..#....__##
##p.***.o._.#
#..#........#
#....#.....##
###........##
###.......###
###.......###
#######...###
#############

( Introduces momentum transfer to stop over a pit )

############
#####..#####
#####......#
###.....#..#
###.*......#
#....o..p..#
#.*..._...##
####...#..##
####.*....##
####.....###
############

( Introduces momentum transfer of multiple balls )

###############
#.....o......##
#p####.#####.##
#..**...***..o#
######.#####.##
######.......##
###############

#####################
###p.**..o*....*..###
###.........._....###
##########.##########
##..***.*.o.o**.*..##
##.................##
##########.##########
#..****.**.*.o....*.#
#...................#
#####################

##############
###..###...###
###.........##
##.......*..##
##.........###
#..##...######
#..#..####___#
#....._***.o_#
#...*.####___#
###.....######
###..#.p.#####
####.....#####
##############

###############
#####_#_#_#####
#####o#o#o#####
#####o#o#o#####
#####o#o#o#####
##..#o#o#o#..##
##...........##
##.#.*****.#.##
#.........#...#
#.p..*****....#
#...#.........#
###..*****..###
####.......####
###############
###############

( Introduces sand )

###########
#..#.o...##
#......p.##
##........#
#...~.....#
#...~~..#.#
###...*...#
###.....###
###########

###########
#...#o#~~.#
#.~~..~~..#
#.._..~~.##
##.~~._..##
##~~..p.~.#
#.*.~~__~.#
##....__..#
###########

############
#...##.....#
#.*......~.#
#.....#..~.#
#.##.__.*..#
#...._~...o#
#....__~.p.#
##.~~....._#
##....######
############

( Introduces interaction of sand with multiple balls )

###########
#...#######
#.#....*..#
#...##.##.#
##.....~..#
####*__.###
##.p..*...#
##....o...#
###########

#############
####p.~~#####
##....~~~.###
##...*....###
#...*.......#
#.#....~..o_#
#...##...#__#
#############

( Introduces metal balls )

#############
###..####..##
#....#.....##
#...p..#....#
#.x..o*q*...#
#...#.....###
#############

############
##..#_###..#
##.x._***..#
#.p........#
#....##....#
###..#####o#
############

##########
#...######
#.#.....##
#...*....#
#p#..____#
#..*._x.o#
##...____#
##.#.....#
##...#####
##########

#############
##......#####
##.###....#_#
##.***.oooo_#
#...p......_#
#.....x....##
#####......##
#############

message Thank you for playing! :)

(

########################
#O.P..***.****.*._.*...#
########################

)